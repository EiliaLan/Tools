# Script by Bartlomiej Duda (Ikskoks)
# AFK Arena (Android) LZAP script


# Ver    Date        Author               Comment
# v0.1   21.08.2022  Bartlomiej Duda      -
# v0.2   21.08.2022  Bartlomiej Duda      -


comtype lz4
idstring "LZ4PNG"
get ARCHIVE_SIZE asize
Set FILE_COUNT long 0

for i = 0
	
	SavePos CURR_OFFSET
	
	if CURR_OFFSET >= ARCHIVE_SIZE
		cleanexit
	endif
	
	endian big
	get FILE_COMP_SIZE long
	
	if FILE_COMP_SIZE == 0
		cleanexit
	endif
	
	XMATH FILE_COMP_SIZE "FILE_COMP_SIZE - 4"
	endian little
	get FILE_UNCOMP_SIZE long
	
	get FILE_NAME basename

	SavePos FILE_OFFSET

	XMATH FILE_COUNT "FILE_COUNT+1"
	set F_OUT_NAME string FILE_NAME
	string F_OUT_NAME += "_"
	string F_OUT_NAME += FILE_COUNT
	string F_OUT_NAME += "_out.pkm"

	clog F_OUT_NAME FILE_OFFSET FILE_COMP_SIZE FILE_UNCOMP_SIZE
	
	XMATH NEXT_ENTRY_OFFSET "FILE_OFFSET + FILE_COMP_SIZE"
	goto NEXT_ENTRY_OFFSET

next i