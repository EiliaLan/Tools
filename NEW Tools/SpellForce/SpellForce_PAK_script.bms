# Script by Bartlomiej Duda (Ikskoks)
# SpellForce 1 (PC) PAK script

# Tested on SpellForce: Platinum Edition from GOG.com


# Ver    Date        Author               Comment
# v0.1   27.10.2022  Bartlomiej Duda      -
# v0.2   28.10.2022  Bartlomiej Duda      Refactor



get VERSION_NUMBER long
if VERSION_NUMBER != 4
 	print "Version not supported! Exiting!
	cleanexit
endif

GetDString VERSION_STRING 24
if VERSION_STRING != "MASSIVE PAKFILE V 4.0\r\n\0"
	print "Invalid signature! Exiting!"
	cleanexit
endif


goto 76


get NUMBER_OF_FILES long
get ROOT_INDEX long
get DATA_START_OFFSET long
get ARCHIVE_SIZE long


get ARCHIVE_NAME basename
set OUTPUT_FILE_PATH string ARCHIVE_NAME


set HEADER_SIZE long 92
set DIRECTORY_ENTRY_SIZE long 16
XMATH BASE_NAME_LIST_OFFSET "HEADER_SIZE + (NUMBER_OF_FILES * DIRECTORY_ENTRY_SIZE)"

for i = 0 < NUMBER_OF_FILES
	get FILE_SIZE long
	get FILE_OFFSET long
	get FILE_NAME_OFFSET long
	get DIRECTORY_NAME_OFFSET long
	
	XMATH FILE_OFFSET "FILE_OFFSET + DATA_START_OFFSET"
	XMATH FILE_NAME_OFFSET "BASE_NAME_LIST_OFFSET + (FILE_NAME_OFFSET & 0xffffff) + 2"
	XMATH DIRECTORY_NAME_OFFSET "BASE_NAME_LIST_OFFSET + (DIRECTORY_NAME_OFFSET & 0xffffff)"
	
	SavePos PATH_BACK_OFFSET
	
	goto DIRECTORY_NAME_OFFSET
	get DIRECTORY_NAME string
	string DIRECTORY_NAME r DIRECTORY_NAME
	
	goto FILE_NAME_OFFSET
	get FILE_NAME string
	string FILE_NAME r FILE_NAME
	
	string OUTPUT_FILE_PATH p "%s/%s/%s" ARCHIVE_NAME DIRECTORY_NAME FILE_NAME
	
	log OUTPUT_FILE_PATH FILE_OFFSET FILE_SIZE
	set OUTPUT_FILE_PATH string ARCHIVE_NAME
	goto PATH_BACK_OFFSET
	
next i
