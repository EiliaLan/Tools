# Script by Bartlomiej Duda (Ikskoks)
# Deadly Premonition (X360) XPC script


# Ver    Date        Author               Comment
# v0.1   13.09.2022  Bartlomiej Duda      -


comtype zlib
idstring "XPC2"
get ARCHIVE_SIZE long
get TEX_COUNT short
get MAT_COUNT short
get USED_TEX_COUNT short
GetDString TEMP 18
get INDEX_START_OFFSET long
get DATA_OFFSET long

XMATH TOTAL_FILE_COUNT "MAT_COUNT * USED_TEX_COUNT"
goto INDEX_START_OFFSET
set FILE_COUNT long 0

for i = 0 < TOTAL_FILE_COUNT

	SavePos CURR_OFFSET
 	GetDString ORIGINAL_NAME 16
	get FILE_OFFSET long
	get FILE_COMP_SIZE long
	get FILE_TYPE long
	get FILE_UNCOMP_SIZE long
	
	
	if FILE_COMP_SIZE != 0
		XMATH FILE_COUNT "FILE_COUNT + 1"
		if ORIGINAL_NAME == ""
 			set F_OUT_NAME string "file"
			string F_OUT_NAME += "_"
			string F_OUT_NAME += FILE_COUNT
			string F_OUT_NAME += ".dds"
		else
			set F_OUT_NAME string "file"
			string F_OUT_NAME += "_"
			string F_OUT_NAME += FILE_COUNT
			string F_OUT_NAME += "_"
			string F_OUT_NAME +=  ORIGINAL_NAME
			string F_OUT_NAME += ".dds"
		endif
		
		#print "curr: %CURR_OFFSET%, f_offset: %FILE_OFFSET%, f_comp_size: %FILE_COMP_SIZE%, f_type: %FILE_TYPE%, f_uncomp_size: %FILE_UNCOMP_SIZE%"
	
		if FILE_UNCOMP_SIZE != 0
			clog F_OUT_NAME FILE_OFFSET FILE_COMP_SIZE FILE_UNCOMP_SIZE
		else
			log F_OUT_NAME FILE_OFFSET FILE_COMP_SIZE
		endif
	endif
next i
