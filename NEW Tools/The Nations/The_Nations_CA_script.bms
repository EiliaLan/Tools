# Script by Bartlomiej Duda (Ikskoks)
# The Nations (PC) CA script


# Ver    Date        Author               Comment
# v0.1   31.05.2022  Bartlomiej Duda      -



# get info from header
idstring "binary.archive00"
goto 0
GetDString SIGNATURE 24
get INDEX_OFFSET long
get NULLS long
GetDString COPYRIGHT_NOTICE 24
get UNKNOWN longlong


# parse index and extract data
get ARCHIVE_SIZE asize
goto INDEX_OFFSET
XMATH INDEX_LENGTH "ARCHIVE_SIZE - INDEX_OFFSET"
XMATH NUMBER_OF_ENTRIES "INDEX_LENGTH / 64"
Set FILE_COUNT long 0


for i = 0 < NUMBER_OF_ENTRIES
	GetDString FILE_NAME 16
	GetDString FILE_TYPE 4
	get FILE_SIZE long
	get FILE_OFFSET long
	GetDString NULLS 20
	GetDString UNKNOWN 8
	GetDString NULLS 8
	
	XMATH FILE_COUNT "FILE_COUNT+1"
	set F_OUT_NAME string "file"
	string F_OUT_NAME += FILE_COUNT
	string F_OUT_NAME += "_"
	string F_OUT_NAME += FILE_TYPE
	string F_OUT_NAME += "_"
	string F_OUT_NAME += FILE_NAME
	
	log F_OUT_NAME FILE_OFFSET FILE_SIZE
next i
